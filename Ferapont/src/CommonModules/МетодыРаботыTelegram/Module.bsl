#Область СлужебныйПрограммныйИнтерфейс

// Отправить картинку.
// 
// Параметры:
//  Сообщество - СправочникСсылка.Сообщества - Сообщество
//  Текст - Строка - Текст
//  Картинка - ДвоичныеДанные -Картинка
// 
// Возвращаемое значение:
//  Произвольный -  Ответ Telegram
Функция ОтправитьКартинку(Знач Сообщество, Знач Текст, Знач Картинка) Экспорт
    
    РеквизитыСообщества = ЗначенияПовтИсп.КэшируемыеПараметрыСообществаТелеграм(Сообщество);
    
    Параметры = Новый Структура;
    Параметры.Вставить("caption"            , Текст); 
    Параметры.Вставить("protect_content"    , "true");
    Параметры.Вставить("token"              , РеквизитыСообщества.ТГ_token);
    Параметры.Вставить("chat_id"            , РеквизитыСообщества.ТГ_chat_id);
    
    СтруктураФайлов = Новый Структура;
    СтруктураФайлов.Вставить("photo",  Картинка);
    
    Ответ = МетодыРаботыHttp.Post("https://api.telegram.org/bot" + Параметры.token + "/sendPhoto"
        , Параметры
        , СтруктураФайлов); 

    Возврат Ответ;
    
КонецФункции

// Отправить группу картинок.
// 
// Параметры:
//  Сообщество - СправочникСсылка.Сообщества - Сообщество
//  Текст - Строка - Текст
//  МассивКартинок - Массив из ДвоичныеДанные - Массив картинок
// 
// Возвращаемое значение:
//  Произвольный -  Отправить группу картинок
Функция ОтправитьГруппуКартинок(Знач Сообщество, Знач Текст, Знач МассивКартинок) Экспорт
    
    РеквизитыСообщества = ЗначенияПовтИсп.КэшируемыеПараметрыСообществаТелеграм(Сообщество);
    
    Параметры    = Новый Структура;
    photo        = "photo";
    
    Параметры.Вставить("protect_content", "true");
    Параметры.Вставить("token"          , РеквизитыСообщества.ТГ_token);
    Параметры.Вставить("chat_id"        , РеквизитыСообщества.ТГ_chat_id);

    Медиа             = Новый Массив;    
    СтруктураФайлов   = Новый Структура;
    Счетчик           = 0;
    
    Для Каждого КартинкаМассива Из МассивКартинок Цикл
        
        ФайлСуществует = Ложь;
        
        Если ТипЗнч(КартинкаМассива) = Тип("Строка") Тогда
            ФайлПути        = Новый Файл(КартинкаМассива);
            ФайлСуществует  = ФайлПути.Существует();
        Иначе
            ФайлСуществует  = Истина;
        КонецЕсли;

        Если ФайлСуществует Тогда
            
            СтруктураФайлов.Вставить(photo + Строка(Счетчик),  КартинкаМассива);
            
            СтруктураМедиа = Новый Структура;
            СтруктураМедиа.Вставить("type"    , photo);
            СтруктураМедиа.Вставить("media"   , "attach://" + photo + Строка(Счетчик));
            
            Если Счетчик = 0 Тогда
                СтруктураМедиа.Вставить("caption", Текст);
            КонецЕсли;
            
            Медиа.Добавить(СтруктураМедиа);
            
            Счетчик = Счетчик + 1;
            
        КонецЕсли;
                
    КонецЦикла;

    Параметры.Вставить("media", МетодыРаботыHttp.JSONСтрокой(Медиа));
    
    Ответ = МетодыРаботыHttp.Post("https://api.telegram.org/bot" + Параметры.token + "/sendMediaGroup"
        , Параметры
        , СтруктураФайлов); 

    Возврат Ответ;

КонецФункции

// Отправить сообщение.
// 
// Параметры:
//  IDЧата - Число,Строка - ID чата Telegram
//  Текст - Строка - Текст
//  Клавиатура - Строка -  Клавиатура в формате JSON
// 
// Возвращаемое значение:
//  Произвольный -  Отправить сообщение
Функция ОтправитьСообщение(Знач IDЧата, Знач Текст, Знач Клавиатура = "") Экспорт
    
    МетодыРаботыHttp.ЗаменитьСпецсимволы(Текст);
    IDЧата 		= МетодыРаботыHttp.ЧислоВСтроку(IDЧата);
    Сообщество  = ПредопределенноеЗначение("Справочник.Сообщества.Athenaeum");
    Токен  		= ЗначенияПовтИсп.КэшируемыйТокенБотаТелеграм(Сообщество); 

    Если Не ЗначениеЗаполнено(Клавиатура) Тогда
       
		Клавиатура = ЗначенияПовтИсп.КэшируемаяКлавиатураПереходов();
    
    КонецЕсли;
    
    _Параметры = Новый Структура;
    _Параметры.Вставить("parse_mode"    , "Markdown");
    _Параметры.Вставить("text"          , Текст);
    _Параметры.Вставить("chat_id"       , IDЧата);
    _Параметры.Вставить("reply_markup"  , Клавиатура);
    
    Ответ = МетодыРаботыHttp.Get("api.telegram.org/bot" + Токен + "/sendMessage", _Параметры);
            
    Возврат Ответ;
    
КонецФункции

// Сформировать клавиатуру по массиву кнопок.
// 
// Параметры:
//  МассивКнопок - Массив из Строка - Массив названий = значений кнопок
//  ПодСообщением - Булево -  Под сообщением или как панель
// 
// Возвращаемое значение:
//  Строка -  JSON
Функция СформироватьКлавиатуруПоМассивуКнопок(Знач МассивКнопок, Знач ПодСообщением = Ложь) Экспорт
    
	Возврат ЗначенияПовтИсп.КэшируемаяКлавиатура(МассивКнопок, ПодСообщением);
    
КонецФункции

Функция ОтправитьАудио(Знач Сообщество, Знач Аудио, Знач Текст = "", Знач Клавиатура = "") Экспорт
	
	РеквизитыСообщества = ЗначенияПовтИсп.КэшируемыеПараметрыСообществаТелеграм(Сообщество);
	Токен				= РеквизитыСообщества.ТГ_token;
	IDЧата				= РеквизитыСообщества.ТГ_chat_id;
	
    Возврат ОтправитьФайл(Токен, IDЧата, Текст, Аудио, "audio", Клавиатура);
    
КонецФункции

Функция ОтправитьФайл(Знач Токен, Знач IDЧата, Знач Текст, Знач Файл, Знач Вид, Знач Клавиатура) Экспорт
    
    Если Вид = "photo" Тогда
        Метод = "/sendPhoto";
    ИначеЕсли Вид = "video" Тогда
        Метод = "/sendVideo";
    ИначеЕсли Вид = "audio" Тогда
        Метод = "/sendAudio";
    ИначеЕсли Вид = "document" Тогда
        Метод   = "/sendDocument";
    ИначеЕсли Вид = "animation" Тогда
        Метод = "/sendAnimation";
    Иначе 
        Возврат "";
    КонецЕсли;
    
    МетодыРаботыHttp.ЗаменитьСпецСимволы(Текст);
    IDЧата = МетодыРаботыHttp.ЧислоВСтроку(IDЧата);
    
    Если Не ТипЗнч(Файл) = Тип("ДвоичныеДанные") Тогда      
        ТекущийФайл = Новый Файл(Файл);
        Расширение  = ?(Вид = "document", ТекущийФайл.Расширение, "");
        Файл        = Новый ДвоичныеДанные(Файл);   
    Иначе
        Расширение  = "";
    КонецЕсли;
    
    Расширение = СтрЗаменить(Расширение, ".", "___");
    
    _Параметры = Новый Структура;
    _Параметры.Вставить("parse_mode"    , "Markdown");
    _Параметры.Вставить("caption"       , Текст);
    _Параметры.Вставить("chat_id"       , IDЧата);
    _Параметры.Вставить("reply_markup"  , Клавиатура);
    
    СтруктураФайлов = Новый Структура;
    СтруктураФайлов.Вставить(Вид + Расширение,  Файл);
    
    Ответ = МетодыРаботыHttp.Post("api.telegram.org/bot" 
    + Токен 
    + Метод, _Параметры, СтруктураФайлов, "mixed"); 
   
    Возврат Ответ;
    
КонецФункции

Функция ПереслатьСообщение(Знач Сообщество, Знач IDОригинала) Экспорт
	
	РеквизитыСообщества = ЗначенияПовтИсп.КэшируемыеПараметрыСообществаТелеграм(Сообщество);
	   
	IDОригинала			= МетодыРаботыHttp.ЧислоВСтроку(IDОригинала);
	Токен				= РеквизитыСообщества.ТГ_token;
	КудаID				= РеквизитыСообщества.ТГ_chat_id;
	ОткудаID			= РеквизитыСообщества.ТГ_КаналАрхив;

    _Параметры = Новый Структура;
    _Параметры.Вставить("chat_id"       , КудаID);
    _Параметры.Вставить("from_chat_id"  , ОткудаID);
    _Параметры.Вставить("message_id"    , IDОригинала);
    
    Ответ = МетодыРаботыHttp.Get("api.telegram.org/bot" + Токен + "/forwardMessage", _Параметры);
    
    Возврат Ответ;

КонецФункции

#КонецОбласти
