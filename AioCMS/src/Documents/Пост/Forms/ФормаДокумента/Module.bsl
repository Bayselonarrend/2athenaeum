#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализацияСодержания();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Пост.Сообщество КАК Сообщество,
		|	Пост.ВидКартинки КАК ВидКартинки,
		|	Пост.Уровень КАК Уровень
		|ИЗ
		|	Документ.Пост КАК Пост
		|
		|УПОРЯДОЧИТЬ ПО
		|	Пост.Дата УБЫВ";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Объект.Сообщество = ВыборкаДетальныеЗаписи.Сообщество;
			Объект.ВидКартинки = ВыборкаДетальныеЗаписи.ВидКартинки;
			Объект.Уровень = ВыборкаДетальныеЗаписи.Уровень;
		КонецЕсли;
				
	КонецЕсли;
		
	ОбновитьКартинкуНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КартинкаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнструментарийКлиент.ЗаписатьФайлВРеквизит(Объект.Ссылка, ЭтотОбъект, "КартинкаДвоичные", "Картинка"); 

КонецПроцедуры

&НаКлиенте
Процедура СодержаниеПриИзменении(Элемент)
	СодержаниеПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Промт(Команда)
	ПромтНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКартинку(Команда)
	ОбновитьКартинкуНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализацияСодержания()
	
	Если Не ЗначениеЗаполнено(Объект.ВидКартинки) Тогда
		Объект.ВидКартинки = Перечисления.ВидыКартинок.Одиночная;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Уровень) Тогда
		Объект.Уровень = ИнструментарийВызовСервера.ЗначениеРеквизитаОбъекта(Объект.Сообщество, "СтандартныйУровеньПоста");	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПромтНаСервере()	
	Записать();
	Midjourney = МетодыФормированияПостов.НовыйПромт(Объект.Ссылка);	
КонецПроцедуры

&НаСервере
Процедура СодержаниеПриИзмененииНаСервере()
	
	Записать();
	
	Если ТипЗнч(Объект.Содержание) = Тип("СправочникСсылка.Термины") Тогда
		Объект.ВидКартинки = Перечисления.ВидыКартинок.Термин;
		Объект.Уровень = Перечисления.УровниПостов.II;
	ИначеЕсли ТипЗнч(Объект.Содержание) = Тип("СправочникСсылка.События") Тогда
		Объект.ВидКартинки = Перечисления.ВидыКартинок.Событие;
		Объект.Уровень = Перечисления.УровниПостов.II;
	ИначеЕсли ТипЗнч(Объект.Содержание) = Тип("СправочникСсылка.Аудиозаписи") Тогда
		Объект.ВидКартинки  = Перечисления.ВидыКартинок.Лекция;
		Объект.Уровень		= Перечисления.УровниПостов.III;
	ИначеЕсли ТипЗнч(Объект.Содержание) = Тип("СправочникСсылка.Сложнопост") Тогда
		Объект.ВидКартинки	= Перечисления.ВидыКартинок.ПроизвольныйНабор;
		Объект.Уровень		= Перечисления.УровниПостов.III;		
	ИначеЕсли ТипЗнч(Объект.Содержание) = Тип("СправочникСсылка.Биографии") Тогда
		Объект.ВидКартинки  = Перечисления.ВидыКартинок.Биография;
		Объект.Уровень      = Перечисления.УровниПостов.IV;
	Иначе
		ИнициализацияСодержания();		
	КонецЕсли;

	ТекстПоста = МетодыФормированияПостов.СформироватьТекстПоста(Объект.Содержание);

КонецПроцедуры

&НаСервере
Процедура ОбновитьКартинкуНаСервере()
	
	Попытка

		СтруктураПоста = МетодыФормированияПостов.СформироватьПост(Объект.Ссылка, , , Ложь);
		ТекстПоста     = СтруктураПоста.ТекстПоста;
		
		МассивКартинок = СтруктураПоста.МассивКартинок;
		
		Если Не МассивКартинок.Количество() = 0 Тогда
			
			Картинка = МассивКартинок[0];
			
			Если Не ТипЗнч(Картинка) = Тип("ДвоичныеДанные") Тогда			
				Картинка = Новый ДвоичныеДанные(Картинка); 
			КонецЕсли;
			
			КартинкаПоста = ПоместитьВоВременноеХранилище(Картинка, Новый УникальныйИдентификатор);
			
		КонецЕсли;
		
	Исключение
		ТекстПоста = "";
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти