#Область СлужебныйПрограммныйИнтерфейс

Функция ОтправитьКартинку(Знач Сообщество, Знач Текст, Знач Картинка) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("caption"			, Текст); 
	Параметры.Вставить("protect_content"	, "true");
	Параметры.Вставить("token"				, Сообщество.ТГ_token);
	Параметры.Вставить("chat_id"			, Сообщество.ТГ_chat_id);
	
	СтруктураФайлов = Новый Структура;
	СтруктураФайлов.Вставить("photo",  Картинка);
	
	Ответ = МетодыРаботыHttp.Post("https://api.telegram.org/bot" + Параметры.token + "/sendPhoto", Параметры, СтруктураФайлов); 

	Возврат Ответ;
	
КонецФункции

Функция ОтправитьГруппуКартинок(Знач Сообщество, Знач Текст, Знач МассивКартинок) Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("protect_content", "true");
	Параметры.Вставить("token"				, Сообщество.ТГ_token);
	Параметры.Вставить("chat_id"			, Сообщество.ТГ_chat_id);

	Медиа 			= Новый Массив;	
	СтруктураФайлов = Новый Структура;
	Счетчик			= 0;
	
	Для Каждого КартинкаМассива Из МассивКартинок Цикл
		
		ФайлСуществует = Ложь;
		
		Если ТипЗнч(КартинкаМассива) = Тип("Строка") Тогда
			ФайлПути 		= Новый Файл(КартинкаМассива);
			ФайлСуществует  = ФайлПути.Существует();
		Иначе
			ФайлСуществует = Истина;
		КонецЕсли;

		Если ФайлСуществует Тогда
			
			СтруктураФайлов.Вставить("photo" + Строка(Счетчик),  КартинкаМассива);
			
			СтруктураМедиа = Новый Структура;
			СтруктураМедиа.Вставить("type", "photo");
			СтруктураМедиа.Вставить("media", "attach://" + "photo" + Строка(Счетчик));
			
			Если Счетчик = 0 Тогда
				СтруктураМедиа.Вставить("caption", Текст);
			КонецЕсли;
			
			Медиа.Добавить(СтруктураМедиа);
			
			Счетчик = Счетчик + 1;
			
		КонецЕсли;
				
	КонецЦикла;

	Параметры.Вставить("media", МетодыРаботыHttp.JSONСтрокой(Медиа));
	
	Ответ = МетодыРаботыHttp.Post("https://api.telegram.org/bot" + Параметры.token + "/sendMediaGroup", Параметры, СтруктураФайлов); 

	Возврат Ответ;

КонецФункции

Функция ОтправитьСообщение(Идентификатор, Сообщение, Дополнение = "Страницы", ПользовательИБ) Экспорт
	
	Токен  		= Справочники.Сообщества.Athenaeum.TGB_token; 
	Апи  		= "api.telegram.org";
	
	МассивСимволов = Новый Соответствие;
	МассивСимволов.Вставить("<", "&lt;");
	МассивСимволов.Вставить(">", "&gt;");
	МассивСимволов.Вставить("&", "&amp;");
	МассивСимволов.Вставить("_", " ");
	МассивСимволов.Вставить("[", "(");
	МассивСимволов.Вставить("]", ")");
	
	Для Каждого СимволМассива Из МассивСимволов Цикл
		Сообщение = СтрЗаменить(Сообщение, СимволМассива.Ключ, СимволМассива.Значение);
	КонецЦикла;
	
	Ресурс 	= "bot"                                            
	+ Токен 
	+ "/sendMessage?chat_id=" 
	+ СтрЗаменить(Формат(Идентификатор, "ЧДЦ=; ЧС=; ЧРГ=."), ".", "") 
	+ "&text="
	+ СокрЛП(Сообщение);
	
	Если Дополнение = "Страницы" Тогда
		
		Строки = Новый Массив;
		Кнопки = Новый Массив;
		
		Кнопки.Добавить(Новый Структура("text,callback_data","<-"	,"<-"));
		Кнопки.Добавить(Новый Структура("text,callback_data","->"	,"->"));
		
		Строки.Добавить(Кнопки);
		
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет,,,ЭкранированиеСимволовJSON.СимволыВнеASCII));
		ЗаписатьJSON(ЗаписьJSON, Новый Структура("keyboard,resize_keyboard",Строки, Истина));
		
		КлавВСообщ = ЗаписьJSON.Закрыть();
		
		Ресурс = Ресурс 
		+ "&parse_mode=Markdown"
		+ "&reply_markup=" + КлавВСообщ;
		
		
	ИначеЕсли Дополнение = "Скрыть" Тогда
		
		Ресурс = Ресурс
		+ "&parse_mode=Markdown"
		+ "&reply_markup={""hide_keyboard"":true}";
		
	КонецЕсли;
	
	Соединение  = Новый HTTPСоединение(Апи,443,,,,100,Новый ЗащищенноеСоединениеOpenSSL());
	Запрос 		= Новый HTTPЗапрос(Ресурс);
	
	Ответ = Соединение.Получить(Запрос);  
	
	ИнструментарийВызовСервера.ЗаписьВОтветыАпи(Ответ.ПолучитьТелоКакСтроку()
	,Перечисления.СоцСети.Telegram
	,Апи);
	
	РегистрыСведений.Диалоги.ЗаписатьСообщениеДиалога(
	Справочники.Пользователи.Ферапонт
	, ПользовательИБ
	, Перечисления.СоцСети.Telegram
	, Сообщение);
	
	Возврат Ответ.ПолучитьТелоКакСтроку();
	
КонецФункции

#КонецОбласти
