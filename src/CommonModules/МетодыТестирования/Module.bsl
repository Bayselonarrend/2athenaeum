#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаписатьТест(Запрос, Метод) Экспорт
	
	Попытка
		
		Если Не Константы.ЗаписыватьТесты.Получить() Тогда
			Возврат;
		КонецЕсли;
		
		НовыйТест = Справочники.АвтотестыСервисов.СоздатьЭлемент();
		НовыйТест.Наименование 	= "Тест " + Строка(Новый УникальныйИдентификатор);
		НовыйТест.ВходнойJSON	= МетодыРаботыHttp.JSONСтрокой(Запрос.ПолучитьТелоКакСтроку());
		НовыйТест.ХранилищеЗаголовков = Новый ХранилищеЗначения(Запрос.Заголовки);
		НовыйТест.ХранилищеПараметров = Новый ХранилищеЗначения(Запрос.ПараметрыЗапроса);
		НовыйТест.Метод = Метод;
		НовыйТест.URL	= Запрос.БазовыйURL + Запрос.ОтносительныйURL;
		НовыйТест.Записать();
		
	Исключение
		ИнструментарийВызовСервера.ЗаписьВЖурналИсключений(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

Функция ЗапускТеста(Тест) Экспорт
	
	РеквизитыТеста 	= ИнструментарийВызовСервера.ЗначенияРеквизитовОбъекта(Тест, "ВходнойJSON,Метод,URL");
	JSONЗапроса		= РеквизитыТеста.ВходнойJSON;
	URL				= РеквизитыТеста.URL;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АвтотестыСервисовЗаменяемыеПараметры.Параметр КАК Параметр,
		|	АвтотестыСервисовЗаменяемыеПараметры.Значение КАК Значение
		|ИЗ
		|	Справочник.АвтотестыСервисов.ЗаменяемыеПараметры КАК АвтотестыСервисовЗаменяемыеПараметры
		|ГДЕ
		|	АвтотестыСервисовЗаменяемыеПараметры.Ссылка = &Тест";
	
	Запрос.УстановитьПараметр("Тест", Тест);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		JSONЗапроса = СтрЗаменить(JSONЗапроса, "@" + ВыборкаДетальныеЗаписи.Параметр, ВыборкаДетальныеЗаписи.Значение);  
	КонецЦикла;
	
	Заголовки 			= Тест.ХранилищеЗаголовков.Получить();	
	ПараметрыЗапроса 	= Тест.ХранилищеПараметров.Получить();	
	
	Ответ = МетодыРаботыHttp.ЗапросПоПараметрамСервиса(Заголовки, ПараметрыЗапроса, URL, JSONЗапроса); 
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти