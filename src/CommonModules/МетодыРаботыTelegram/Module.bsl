#Область СлужебныйПрограммныйИнтерфейс

// Отправить картинку.
// 
// Параметры:
//  Сообщество - СправочникСсылка.Сообщества - Сообщество
//  Текст - Строка - Текст
//  Картинка - ДвоичныеДанные -Картинка
// 
// Возвращаемое значение:
//  Произвольный -  Ответ Telegram
Функция ОтправитьКартинку(Знач Сообщество, Знач Текст, Знач Картинка) Экспорт
    
    РеквизитыСообщества = ИнструментарийВызовСервера.ЗначенияРеквизитовОбъекта(Сообщество, "ТГ_token,ТГ_chat_id");
    
    Параметры = Новый Структура;
    Параметры.Вставить("caption"            , Текст); 
    Параметры.Вставить("protect_content"    , "true");
    Параметры.Вставить("token"              , РеквизитыСообщества.ТГ_token);
    Параметры.Вставить("chat_id"            , РеквизитыСообщества.ТГ_chat_id);
    
    СтруктураФайлов = Новый Структура;
    СтруктураФайлов.Вставить("photo",  Картинка);
    
    Ответ = МетодыРаботыHttp.Post("https://api.telegram.org/bot" + Параметры.token + "/sendPhoto"
        , Параметры
        , СтруктураФайлов); 

    Возврат Ответ;
    
КонецФункции

// Отправить группу картинок.
// 
// Параметры:
//  Сообщество - СправочникСсылка.Сообщества - Сообщество
//  Текст - Строка - Текст
//  МассивКартинок - Массив из ДвоичныеДанные - Массив картинок
// 
// Возвращаемое значение:
//  Произвольный -  Отправить группу картинок
Функция ОтправитьГруппуКартинок(Знач Сообщество, Знач Текст, Знач МассивКартинок) Экспорт
    
    РеквизитыСообщества = ИнструментарийВызовСервера.ЗначенияРеквизитовОбъекта(Сообщество, "ТГ_token,ТГ_chat_id");
    
    Параметры    = Новый Структура;
    photo        = "photo";
    
    Параметры.Вставить("protect_content", "true");
    Параметры.Вставить("token"          , РеквизитыСообщества.ТГ_token);
    Параметры.Вставить("chat_id"        , РеквизитыСообщества.ТГ_chat_id);

    Медиа             = Новый Массив;    
    СтруктураФайлов   = Новый Структура;
    Счетчик           = 0;
    
    Для Каждого КартинкаМассива Из МассивКартинок Цикл
        
        ФайлСуществует = Ложь;
        
        Если ТипЗнч(КартинкаМассива) = Тип("Строка") Тогда
            ФайлПути        = Новый Файл(КартинкаМассива);
            ФайлСуществует  = ФайлПути.Существует();
        Иначе
            ФайлСуществует  = Истина;
        КонецЕсли;

        Если ФайлСуществует Тогда
            
            СтруктураФайлов.Вставить(photo + Строка(Счетчик),  КартинкаМассива);
            
            СтруктураМедиа = Новый Структура;
            СтруктураМедиа.Вставить("type"    , photo);
            СтруктураМедиа.Вставить("media"   , "attach://" + photo + Строка(Счетчик));
            
            Если Счетчик = 0 Тогда
                СтруктураМедиа.Вставить("caption", Текст);
            КонецЕсли;
            
            Медиа.Добавить(СтруктураМедиа);
            
            Счетчик = Счетчик + 1;
            
        КонецЕсли;
                
    КонецЦикла;

    Параметры.Вставить("media", МетодыРаботыHttp.JSONСтрокой(Медиа));
    
    Ответ = МетодыРаботыHttp.Post("https://api.telegram.org/bot" + Параметры.token + "/sendMediaGroup"
        , Параметры
        , СтруктураФайлов); 

    Возврат Ответ;

КонецФункции

// Отправить сообщение.
// 
// Параметры:
//  IDЧата - Число,Строка - ID чата Telegram
//  Текст - Строка - Текст
//  Клавиатура - Строка -  Клавиатура в формате JSON
// 
// Возвращаемое значение:
//  Произвольный -  Отправить сообщение
Функция ОтправитьСообщение(Знач IDЧата, Знач Текст, Знач Клавиатура = "") Экспорт
    
    МетодыРаботыHttp.ЗаменитьСпецсимволы(Текст);
    IDЧата = МетодыРаботыHttp.ЧислоВСтроку(IDЧата);
    Токен  = ИнструментарийВызовСервера.ЗначениеРеквизитаОбъекта(Справочники.Сообщества.Athenaeum, "TGB_token"); 

    Если Не ЗначениеЗаполнено(Клавиатура) Тогда
       
        Строки = Новый Массив;
        Кнопки = Новый Массив;
        
        Кнопки.Добавить(Новый Структура("text,callback_data", "<-"    , "<-"));
        Кнопки.Добавить(Новый Структура("text,callback_data", "->"    , "->"));
        
        Строки.Добавить(Кнопки);
        
        ЗаписьJSON    = Новый ЗаписьJSON;
        ПЗJSON        = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет, , , ЭкранированиеСимволовJSON.СимволыВнеASCII);
        ЗаписьJSON.УстановитьСтроку(ПЗJSON);
            
        ЗаписатьJSON(ЗаписьJSON, Новый Структура("keyboard,resize_keyboard", Строки, Истина));
        
        Клавиатура = ЗаписьJSON.Закрыть();
    
    КонецЕсли;
    
    _Параметры = Новый Структура;
    _Параметры.Вставить("parse_mode"    , "Markdown");
    _Параметры.Вставить("text"          , Текст);
    _Параметры.Вставить("chat_id"       , IDЧата);
    _Параметры.Вставить("reply_markup"  , Клавиатура);
    
    Ответ = МетодыРаботыHttp.Get("api.telegram.org/bot" + Токен + "/sendMessage", _Параметры);
        
    РегистрыСведений.Диалоги.ЗаписатьСообщениеДиалога(Справочники.Пользователи.Ферапонт
        , Справочники.Пользователи.НайтиПоРеквизиту("Telegram", IDЧата)
        , Перечисления.СоцСети.Telegram
        , Текст);
    
    Возврат Ответ;
    
КонецФункции

// Сформировать клавиатуру по массиву кнопок.
// 
// Параметры:
//  МассивКнопок - Массив из Строка - Массив названий = значений кнопок
//  ПодСообщением - Булево -  Под сообщением или как панель
// 
// Возвращаемое значение:
//  Строка -  JSON
Функция СформироватьКлавиатуруПоМассивуКнопок(Знач МассивКнопок, Знач ПодСообщением = Ложь) Экспорт
    
    Строки = Новый Массив;
        
    Для Каждого Кнопка Из МассивКнопок Цикл
        
        Кнопки = Новый Массив;
        Кнопка = КодироватьСтроку(МетодыРаботыHttp.ЧислоВСтроку(Кнопка), СпособКодированияСтроки.КодировкаURL);
        Кнопки.Добавить(Новый Структура("text,callback_data", Кнопка, Кнопка));
        Строки.Добавить(Кнопки);

    КонецЦикла;
    
    Если ПодСообщением Тогда
        СтруктураПараметра = Новый Структура("inline_keyboard,rows", Строки, 1);
    Иначе
        СтруктураПараметра = Новый Структура("keyboard,resize_keyboard", Строки, Истина);
    КонецЕсли;
        
    ЗаписьJSON    = Новый ЗаписьJSON;
    ПЗJSON        = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет, , , ЭкранированиеСимволовJSON.СимволыВнеASCII);
    ЗаписьJSON.УстановитьСтроку(ПЗJSON);
        
    ЗаписатьJSON(ЗаписьJSON, СтруктураПараметра);
    
    Возврат ЗаписьJSON.Закрыть();
    
КонецФункции

#КонецОбласти
