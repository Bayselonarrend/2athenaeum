
#Область ПрограммныйИнтерфейс

Функция СформироватьФайлКниги(Произведение) Экспорт
	
	Текст = Произведение;
	УдалитьВременныеФайлы(Текст);
		
	МассивСтрок 	= Текст.ТекстМассив.Получить();
	
	ИмяМакета		= "Макет";
	Макет 			= Справочники.Произведения.ПолучитьМакет(ИмяМакета);
	Книга			= Новый ТабличныйДокумент;
	ТекущаяСтраница = 1;
	
	Книга.РазмерСтраницы 			= "Quarto";
	Книга.АвтоМасштаб  				= Истина;
	 
	
	ОбластьТитульник 						= Макет.ПолучитьОбласть("Титульник");
	ОбластьТитульник.Параметры.Код 			= Текст.Код;
	ОбластьТитульник.Параметры.Наименование = Текст.Наименование;
	ОбластьТитульник.Параметры.Автор 		= Текст.Автор;
	
	ОбластьШапка 						= Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Наименование = Текст.Наименование;
	ОбластьШапка.Параметры.Автор 		= Текст.Автор.Имя + " " + Текст.Автор.Фамилия;
	ОбластьШапка.Параметры.Страница		= ТекущаяСтраница;
	
	Книга.Вывести(ОбластьТитульник);
	Книга.ВывестиГоризонтальныйРазделительСтраниц();
	Книга.Вывести(ОбластьШапка);
	
	ОбластьСодержания 	= Макет.ПолучитьОбласть("Текст");
	ОбластьСокращенная	= Макет.ПолучитьОбласть("Текст");
	Страница 			= "";
	
	Для Каждого СтрокаТекста Из МассивСтрок Цикл
		
		Пробел = ?(Страница = "", "", " ");
		ОбластьСодержания.Параметры.Содержимое = Страница + Пробел + СтрокаТекста;
		ОбластьСокращенная.Параметры.Содержимое = Страница + Пробел + СокрЛП(СтрокаТекста);

		НоваяСтраница 	= Ложь;
		Вывод 			= Ложь;
		
		Если Не Книга.ПроверитьВывод(ОбластьСодержания) Тогда
			
			НоваяСтраница = Истина;
			
			Если Книга.ПроверитьВывод(ОбластьСокращенная) Тогда
				ОбластьСодержания.Параметры.Содержимое = Страница + " " + СокрЛП(СтрокаТекста);
				Вывод = Истина;
			Иначе
				ОбластьСодержания.Параметры.Содержимое = Страница;
				Вывод = Ложь;
			КонецЕсли;
			
			Книга.Вывести(ОбластьСодержания);

			Страница = "";
			
			Книга.ВывестиГоризонтальныйРазделительСтраниц();
			ТекущаяСтраница = ТекущаяСтраница + 1;
			ОбластьШапка.Параметры.Страница		= ТекущаяСтраница;
			Книга.Вывести(ОбластьШапка);

		КонецЕсли;
		
		Пробел		= ?(НоваяСтраница, "", " ");
		Страница 	= Страница + ?(Вывод, "", Пробел + СтрокаТекста);	
		
	КонецЦикла;
	
	Возврат Книга
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УдалитьВременныеФайлы(Текст)

	МассивРасширений = Новый Массив;
	МассивРасширений.Добавить("pdf");
	МассивРасширений.Добавить("html");
	
	Для Каждого Код Из МассивРасширений Цикл
		КВФ 		= КаталогВременныхФайлов();
		ПутьКФайлу	= КВФ + Текст.Автор + ", " + Текст.Наименование + "." + нРег(Код);
		Файл 		= Новый Файл(ПутьКФайлу);
		
		Если Файл.Существует() Тогда
			УдалитьФайлы(ПутьКФайлу);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
