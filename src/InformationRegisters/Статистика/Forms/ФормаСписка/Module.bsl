#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСтатистику(Команда)
	ОбновитьСтатистикуНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСтатистикуНаСервере()
	
	Дата0 = ПериодОбновления.ДатаНачала;
	Дата1 = ПериодОбновления.ДатаОкончания;
	
	visitors 	= "visitors";
	reach		= "reach";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сообщества.Ссылка КАК Сообщество
	|ИЗ
	|	Справочник.Сообщества КАК Сообщества";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Статистика = МетодыРаботыVK.ПолучитьСтатистику(ВыборкаДетальныеЗаписи.Сообщество, Дата0, Дата1).Получить("response");
		
		Если ЗначениеЗаполнено(Статистика) Тогда
			
			Для Каждого ДеньСтатистики Из Статистика Цикл
				
				МЗ = РегистрыСведений.Статистика.СоздатьМенеджерЗаписи();
				МЗ.Сообщество 	= ВыборкаДетальныеЗаписи.Сообщество;
				МЗ.Период 		= Дата(1970,1,1,1,0,0) + ДеньСтатистики["period_to"];
				
				Если МЗ.Период > ТекущаяДатаСеанса() Тогда
					Продолжить;
				КонецЕсли;
				
				Если ДеньСтатистики.Получить(visitors) <> Неопределено Тогда
					
					Посетители = ДеньСтатистики[visitors];
					МЗ.Посетителей 	= ?(Посетители.Получить(visitors) 	<> Неопределено, Посетители[visitors]	, 0);
					МЗ.Просмотров	= ?(Посетители.Получить("views")	<> Неопределено, Посетители["views"]	, 0);
					
				КонецЕсли;
				
				Если ДеньСтатистики.Получить(reach) <> Неопределено Тогда
					
					Охват = ДеньСтатистики[reach];
					МЗ.Охват 				= ?(Охват.Получить(reach) 				<> Неопределено, Охват[reach]				, 0);
					МЗ.ОхватПодписчиков     = ?(Охват.Получить("reach_subscribers")	<> Неопределено, Охват["reach_subscribers"]	, 0);
					
				Конецесли;
				
				Если ДеньСтатистики.Получить("activity") <> Неопределено Тогда
					
					Активность = ДеньСтатистики["activity"];
					МЗ.Комментарии 		= ?(Активность.Получить("comments")		<> Неопределено	, Активность["comments"]		, 0);
					МЗ.Репосты    		= ?(Активность.Получить("copies")		<> Неопределено	, Активность["copies"]			, 0);
					МЗ.Скрытия    		= ?(Активность.Получить("hidden")		<> Неопределено	, Активность["hidden"]			, 0);
					МЗ.Лайки    		= ?(Активность.Получить("likes")		<> Неопределено	, Активность["likes"]			, 0);
					МЗ.Подписки    		= ?(Активность.Получить("subscribed")	<> Неопределено	, Активность["subscribed"]		, 0);
					МЗ.Отписки    		= ?(Активность.Получить("unsubscribed ")<> Неопределено	, Активность["unsubscribed "]	, 0);
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(МЗ.Охват) Тогда
					МЗ.Записать(Истина);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти


