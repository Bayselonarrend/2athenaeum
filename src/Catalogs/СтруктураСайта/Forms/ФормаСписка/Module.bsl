#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПапкаСайта 	= Константы.ПапкаСайта.Получить();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Собрать(Команда)
	СобратьНаСервере(ПапкаСайта);	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Высадка(Команда)
	ВысадкаНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СобратьНаСервере(ПапкаСайта)
	
	ИсключаемыеКаталоги = Новый Массив;
	ИсключаемыеКаталоги.Добавить(".scannerwork");
	
	Если Не СтрЗаканчиваетсяНа(ПапкаСайта, "\") Тогда
		ПапкаСайта = ПапкаСайта + "\";	
	КонецЕсли;
	
	Выборка 	= Справочники.СтруктураСайта.Выбрать();
	МассивСайта = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		МассивСайта.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	УдалитьОбъекты(МассивСайта);
	
	Корень = НайтиФайлы(ПапкаСайта,"*",Истина);
	СоответствиеКаталогов 		= Новый Соответствие;
	МассивИсключаемыхИндесов    = Новый Массив;
	Счетчик						= 0;
	
	Для Каждого ЭлементКорня Из Корень Цикл
		
		Для Каждого ИсключаемоеИмя Из ИсключаемыеКаталоги Цикл
			Если СтрНайти(ЭлементКорня.ПолноеИмя, ИсключаемоеИмя) > 0 Тогда
				МассивИсключаемыхИндесов.Вставить(0, Счетчик); 		
			КонецЕсли;			
		КонецЦикла;
		
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
	Для Каждого ИсключаемыИндекс Из МассивИсключаемыхИндесов Цикл
		Корень.Удалить(ИсключаемыИндекс);
	КонецЦикла;
		
	Для Каждого Каталог Из Корень Цикл
		
		Если ЗначениеЗаполнено(Каталог.Расширение) И ЗначениеЗаполнено(Каталог.ИмяБезРасширения)  Тогда 
			Продолжить;
		Иначе
			ПутьКаталога 			= СтрЗаменить(Каталог.ПолноеИмя, ПапкаСайта, "");
			МассивПодкаталогов  	= СтрРазделить(ПутьКаталога, "\", Ложь);
			ПредыдущийПодкаталог    = "";
			Счетчик					= 0;
	
			Для Каждого Подкаталог Из МассивПодкаталогов Цикл
				
				ПолныйПутьКаталога = "";
				
				Для Н = 0 По Счетчик Цикл
			
					ПолныйПутьКаталога = ПолныйПутьКаталога + МассивПодкаталогов[Н] + "\";

				КонецЦикла;

				СуществующийКаталог = СоответствиеКаталогов.Получить(ПолныйПутьКаталога);
				
				
				Если СуществующийКаталог <> Неопределено Тогда
					ПредыдущийПодкаталог = СуществующийКаталог;
				Иначе
					НовыйПодкаталог = Справочники.СтруктураСайта.СоздатьГруппу();
					НовыйПодкаталог.Наименование 	= Подкаталог;
					НовыйПодкаталог.Родитель		= ?(ЗначениеЗаполнено(ПредыдущийПодкаталог)
						, ПредыдущийПодкаталог
						, Справочники.СтруктураСайта.ПустаяСсылка());
					НовыйПодкаталог.Записать();
					
					ПредыдущийПодкаталог			= НовыйПодкаталог.Ссылка;
							
					СоответствиеКаталогов.Вставить(ПолныйПутьКаталога, ПредыдущийПодкаталог);
					
				КонецЕсли;
				
				Счетчик = Счетчик + 1;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	                             
	Для Каждого Файл Из Корень Цикл
		Если Не ЗначениеЗаполнено(Файл.Расширение) Или Не ЗначениеЗаполнено(Файл.ИмяБезРасширения)  Тогда 
			Продолжить;
		Иначе
			ПутьБезКорня = СтрЗаменить(Файл.Путь, ПапкаСайта, "");
			
			НовыйФайл = Справочники.СтруктураСайта.СоздатьЭлемент();
			НовыйФайл.Наименование = Файл.Имя; 
			
			Если ЗначениеЗаполнено(ПутьБезКорня) Тогда
				НовыйФайл.Родитель = СоответствиеКаталогов.Получить(ПутьБезКорня);
			КонецЕсли;
					
			НовыйФайл.Путь		= СтрЗаменить(Файл.ПолноеИмя, ПапкаСайта, "");
			НовыйФайл.Хранилище = Новый ХранилищеЗначения(Новый ДвоичныеДанные(Файл.ПолноеИмя), Новый СжатиеДанных(9));
			
			НовыйФайл.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	Константы.ПапкаСайта.Установить(ПапкаСайта);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВысадкаНаСервере()
	
	МассивКаталоговВысадки	= Новый Массив;
	ВыборкаКаталоговВысадки = Справочники.КаталогиВысадки.Выбрать();
	
	Пока ВыборкаКаталоговВысадки.Следующий() Цикл
		
		Если Не СтрЗаканчиваетсяНа(ВыборкаКаталоговВысадки.Наименование, "\") Тогда
			ТекущийКаталог = ВыборкаКаталоговВысадки.Наименование + "\";
		Иначе
			ТекущийКаталог = ВыборкаКаталоговВысадки.Наименование;
		КонецЕсли;
		
		МассивКаталоговВысадки.Добавить(ТекущийКаталог);
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтруктураСайта.Путь КАК Путь,
		|	СтруктураСайта.Хранилище КАК Хранилище
		|ИЗ
		|	Справочник.СтруктураСайта КАК СтруктураСайта
		|ГДЕ
		|	НЕ СтруктураСайта.ЭтоГруппа";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Для Каждого КаталогВыгрузки Из МассивКаталоговВысадки Цикл
			ДД = ВыборкаДетальныеЗаписи.Хранилище.Получить();
			ДД.Записать(КаталогВыгрузки + ВыборкаДетальныеЗаписи.Путь);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти



