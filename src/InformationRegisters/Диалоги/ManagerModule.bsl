#Область ПрограммныйИнтерфейс
#Если НаСервере Тогда
	
// Записать сообщение диалога.
// 
// Параметры:
//  Отправитель - СправочникСсылка.Пользователи - Отправитель
//  Получатель - СправочникСсылка.Пользователи - Получатель
//  СоцСеть - ПеречислениеСсылка.СоцСети - Соц сеть
//  Текст - Строка - Текст сообщения
Процедура ЗаписатьСообщениеДиалога(Знач Отправитель, Знач Получатель, Знач СоцСеть, Знач Текст) Экспорт
	
	Я 	= Справочники.Пользователи.ЭтотПользователь;	
	
	Если Отправитель = Я Или Получатель = Я Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(Текст) <> Тип("Массив") Тогда
		
		ТекстВременно 	= Текст;
		Текст			= Новый Массив;
		Текст.Добавить(ТекстВременно);
		
	КонецЕсли;
	
	Запрос 	= Новый Запрос;
	ТД 		= ТекущаяДатаСеанса();

	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Диалоги.UID КАК UID
	|ИЗ
	|	РегистрСведений.Диалоги КАК Диалоги
	|
	|УПОРЯДОЧИТЬ ПО
	|	Диалоги.UID УБЫВ";
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТД);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		UID = ВыборкаДетальныеЗаписи.UID + 1;
	Иначе
		UID = 1;
	КонецЕсли;
	
	Для Каждого Сообщение Из Текст Цикл
		
		НоваяЗапись				= СоздатьМенеджерЗаписи();
		НоваяЗапись.UID 		= UID;
		НоваяЗапись.Период 		= ТД;
		НоваяЗапись.Отправитель = Отправитель;
		НоваяЗапись.Получатель	= Получатель;
		НоваяЗапись.СоцСеть 	= СоцСеть;
		НоваяЗапись.Текст 		= Сообщение;
		НоваяЗапись.Записать();
		
		UID = UID + 1;
		
	КонецЦикла;

КонецПроцедуры

#КонецЕсли
#КонецОбласти