#Область СлужебныйПрограммныйИнтерфейс

Функция ОтправитьКартинку(Знач Сообщество, Знач Текст, Знач Картинка) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("caption"			, Текст); 
	Параметры.Вставить("protect_content"	, "true");
	Параметры.Вставить("token"				, Сообщество.ТГ_token);
	Параметры.Вставить("chat_id"			, Сообщество.ТГ_chat_id);
	
	СтруктураФайлов = Новый Структура;
	СтруктураФайлов.Вставить("photo",  Картинка);
	
	Ответ = МетодыРаботыHttp.Post("https://api.telegram.org/bot" + Параметры.token + "/sendPhoto", Параметры, СтруктураФайлов); 

	Возврат Ответ;
	
КонецФункции

Функция ОтправитьГруппуКартинок(Знач Сообщество, Знач Текст, Знач МассивКартинок) Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("protect_content", "true");
	Параметры.Вставить("token"				, Сообщество.ТГ_token);
	Параметры.Вставить("chat_id"			, Сообщество.ТГ_chat_id);

	Медиа 			= Новый Массив;	
	СтруктураФайлов = Новый Структура;
	Счетчик			= 0;
	
	Для Каждого КартинкаМассива Из МассивКартинок Цикл
		
		ФайлСуществует = Ложь;
		
		Если ТипЗнч(КартинкаМассива) = Тип("Строка") Тогда
			ФайлПути 		= Новый Файл(КартинкаМассива);
			ФайлСуществует  = ФайлПути.Существует();
		Иначе
			ФайлСуществует = Истина;
		КонецЕсли;

		Если ФайлСуществует Тогда
			
			СтруктураФайлов.Вставить("photo" + Строка(Счетчик),  КартинкаМассива);
			
			СтруктураМедиа = Новый Структура;
			СтруктураМедиа.Вставить("type", "photo");
			СтруктураМедиа.Вставить("media", "attach://" + "photo" + Строка(Счетчик));
			
			Если Счетчик = 0 Тогда
				СтруктураМедиа.Вставить("caption", Текст);
			КонецЕсли;
			
			Медиа.Добавить(СтруктураМедиа);
			
			Счетчик = Счетчик + 1;
			
		КонецЕсли;
				
	КонецЦикла;

	Параметры.Вставить("media", МетодыРаботыHttp.JSONСтрокой(Медиа));
	
	Ответ = МетодыРаботыHttp.Post("https://api.telegram.org/bot" + Параметры.token + "/sendMediaGroup", Параметры, СтруктураФайлов); 

	Возврат Ответ;

КонецФункции

Функция ОтправитьСообщение(Знач IDЧата, Знач Текст, Знач Клавиатура = "") Экспорт
    
    МетодыРаботыHttp.ЗаменитьСпецсимволы(Текст);
    IDЧата = МетодыРаботыHttp.ЧислоВСтроку(IDЧата);
	Токен  = ИнструментарийВызовСервера.ЗначениеРеквизитаОбъекта(Справочники.Сообщества.Athenaeum, "TGB_token"); 

	Если Не ЗначениеЗаполнено(Клавиатура) Тогда
   	
		Строки = Новый Массив;
		Кнопки = Новый Массив;
		
		Кнопки.Добавить(Новый Структура("text,callback_data","<-"	,"<-"));
		Кнопки.Добавить(Новый Структура("text,callback_data","->"	,"->"));
		
		Строки.Добавить(Кнопки);
		
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет,,,ЭкранированиеСимволовJSON.СимволыВнеASCII));
		ЗаписатьJSON(ЗаписьJSON, Новый Структура("keyboard,resize_keyboard",Строки, Истина));
		
		Клавиатура = ЗаписьJSON.Закрыть();
	
	КонецЕсли;
	
	_Параметры = Новый Структура;
	_Параметры.Вставить("parse_mode"    , "Markdown");
	_Параметры.Вставить("text"          , Текст);
	_Параметры.Вставить("chat_id"       , IDЧата);
	_Параметры.Вставить("reply_markup"  , Клавиатура);
	
	
    Ответ = МетодыРаботыHttp.Get("api.telegram.org/bot" + Токен + "/sendMessage", _Параметры);
		
	РегистрыСведений.Диалоги.ЗаписатьСообщениеДиалога(Справочники.Пользователи.Ферапонт
	, Справочники.Пользователи.НайтиПоРеквизиту("Telegram", IDЧата)
	, Перечисления.СоцСети.Telegram
	, Текст);
	
    Возврат Ответ;
    
КонецФункции

Функция СформироватьКлавиатуруПоМассивуКнопок(Знач МассивКнопок, Знач ПодСообщением = Ложь) Экспорт
	
	Строки = Новый Массив;
		
	Для Каждого Кнопка Из МассивКнопок Цикл
		
		Кнопки = Новый Массив;
		Кнопка = МетодыРаботыHttp.ЧислоВСтроку(Кнопка);
		Кнопки.Добавить(Новый Структура("text,callback_data", Кнопка, Кнопка));
		Строки.Добавить(Кнопки);

	КонецЦикла;
	
	
	Если ПодСообщением Тогда
		СтруктураПараметра = Новый Структура("inline_keyboard,rows", Строки, 1);
	Иначе
		СтруктураПараметра = Новый Структура("keyboard,resize_keyboard", Строки, Истина)
	КонецЕсли;
	
		
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет,,,ЭкранированиеСимволовJSON.СимволыВнеASCII));
	ЗаписатьJSON(ЗаписьJSON, СтруктураПараметра);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

#КонецОбласти
