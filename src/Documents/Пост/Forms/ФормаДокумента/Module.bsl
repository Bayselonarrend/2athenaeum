#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализацияСодержания();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Пост.Сообщество КАК Сообщество,
		|	Пост.ВидКартинки КАК ВидКартинки,
		|	Пост.Уровень КАК Уровень
		|ИЗ
		|	Документ.Пост КАК Пост
		|
		|УПОРЯДОЧИТЬ ПО
		|	Пост.Дата УБЫВ";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Объект.Сообщество = ВыборкаДетальныеЗаписи.Сообщество;
			Объект.ВидКартинки = ВыборкаДетальныеЗаписи.ВидКартинки;
			Объект.Уровень = ВыборкаДетальныеЗаписи.Уровень;
		КонецЕсли;
				
	КонецЕсли;
		
	ОбновитьКартинкуНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КартинкаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка 	= Ложь;
	Диалог 					= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок 		= "Выберите файл";
	ОповещениеЗавершения 	= Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтотОбъект);
	Диалог.Показать(ОповещениеЗавершения);

КонецПроцедуры

&НаКлиенте
Процедура СодержаниеПриИзменении(Элемент)
	СодержаниеПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Промт(Команда)
	ПромтНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКартинку(Команда)
	ОбновитьКартинкуНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализацияСодержания()
	
	Если Не ЗначениеЗаполнено(Объект.ВидКартинки) Тогда
		Объект.ВидКартинки = Перечисления.ВидыКартинок.Одиночная;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Уровень) Тогда
		Объект.Уровень = ИнструментарийВызовСервера.ЗначениеРеквизитаОбъекта(Объект.Сообщество, "СтандартныйУровеньПоста");	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда	
		
		Объект.Картинка = ВыбранныеФайлы[0];
		ЗаписатьДвоичныеДанныеКартинки(Новый ДвоичныеДанные(Объект.Картинка));
		Закрыть();
	  	ПерейтиПоНавигационнойСсылке(ПолучитьНавигационнуюСсылку(ЭтотОбъект));

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДвоичныеДанныеКартинки(ДД)
	
	Записать();
	ОбъектПост 					= Объект.Ссылка.ПолучитьОбъект();
	ОбъектПост.КартинкаДвоичные = Новый ХранилищеЗначения(ДД);
	ОбъектПост.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ПромтНаСервере()	
	Записать();
	Midjourney = МетодыФормированияПостов.НовыйПромт(Объект.Ссылка);	
КонецПроцедуры

&НаСервере
Процедура СодержаниеПриИзмененииНаСервере()
	
	Записать();
	
	Если ТипЗнч(Объект.Содержание) = Тип("СправочникСсылка.Термины") Тогда
		Объект.ВидКартинки = Перечисления.ВидыКартинок.Термин;
		Объект.Уровень = Перечисления.УровниПостов.II;
	ИначеЕсли ТипЗнч(Объект.Содержание) = Тип("СправочникСсылка.События") Тогда
		Объект.ВидКартинки = Перечисления.ВидыКартинок.Событие;
		Объект.Уровень = Перечисления.УровниПостов.II;
	Иначе
		ИнициализацияСодержания();		
	КонецЕсли;

	ТекстПоста = МетодыФормированияПостов.СформироватьТекстПоста(Объект.Ссылка);

КонецПроцедуры

&НаСервере
Процедура ОбновитьКартинкуНаСервере()
	
	Попытка

		Если ТипЗнч(Объект.Содержание) = Тип("СправочникСсылка.Термины") Тогда
					
			ТекстПоста 		= ИнструментарийВызовСервера.ЗначениеРеквизитаОбъекта(Объект.Содержание, "Описание");
			ТерминДД		= МетодыОбработкиИзображений.СформироватьТермин(Объект.Содержание);
			КартинкаПоста 	= ПоместитьВоВременноеХранилище(ТерминДД, Новый УникальныйИдентификатор);
			
		ИначеЕсли  ТипЗнч(Объект.Содержание) = Тип("СправочникСсылка.События") Тогда
			
			ТекстПоста 		= МетодыФормированияПостов.СформироватьТекстПоста(Объект.Ссылка);
			СобытиеДД		= МетодыОбработкиИзображений.СформироватьСобытие(Объект.Содержание, Объект.Сообщество);
			КартинкаПоста 	= ПоместитьВоВременноеХранилище(СобытиеДД, Новый УникальныйИдентификатор);
			
		ИначеЕсли  ТипЗнч(Объект.Содержание) = Тип("СправочникСсылка.Цитаты") Тогда
				
			ТекстПоста 			= МетодыФормированияПостов.СформироватьТекстПоста(Объект.Ссылка);
			ДокументОбъект 		= Объект.Ссылка.ПолучитьОбъект();
			ДД					= ДокументОбъект.КартинкаДвоичные.Получить();
			РеквизитыСодержания	= ИнструментарийВызовСервера.ЗначенияРеквизитовОбъекта(Объект.Содержание
				, "ПроизведениеАвтор,Текст");
			
			ИзображениеДД = МетодыОбработкиИзображений.СформироватьИзображениеСТекстом(Объект.Содержание
			, РеквизитыСодержания.Текст
			, ?(ТипЗнч(РеквизитыСодержания.ПроизведениеАвтор) = Тип("СправочникСсылка.Люди")
			, РеквизитыСодержания.ПроизведениеАвтор
			, ИнструментарийВызовСервера.ЗначениеРеквизитаОбъекта(РеквизитыСодержания.ПроизведениеАвтор, "Автор"))
			, ДД
			, Истина);
						
			КартинкаПоста = ПоместитьВоВременноеХранилище(ИзображениеДД, Новый УникальныйИдентификатор);
				
		Иначе
				
			КартинкаПоста = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "КартинкаДвоичные");
			ТекстПоста = МетодыФормированияПостов.СформироватьТекстПоста(Объект.Ссылка);
			
		КонецЕсли;
		
	Исключение
		ТекстПоста = "";
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти